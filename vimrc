"åŸºæœ¬è®¾ç½®======================
set number         "æ˜¾ç¤ºè¡Œå·
syntax on          "è¯­æ³•é«˜äº®
filetype on        "æ£€æŸ¥æ–‡ä»¶ç±»å‹
filetype plugin on
filetype indent on
set tags+=./.tags
set tags+=.tags
set showcmd
"set splitbelow
set splitright
set confirm        "é€€å‡ºä¿å­˜è¯¢é—®
set ruler          "æ‰“å¼€æ ‡å°º
set completeopt=menu,noinsert,preview
set nocompatible   "å…³é—­å…¼å®¹æ¨¡å¼
set autoread       "è®¾ç½®å½“æ–‡ä»¶è¢«æ”¹åŠ¨æ—¶è‡ªåŠ¨è½½å…¥
set nobackup       "ç¦ç”¨å¤‡ä»½
set noswapfile
set cursorline     "é«˜äº®å½“å‰è¡Œ
set autoindent     "è‡ªåŠ¨ç¼©è¿›
"set cindent        "Cç³»åˆ—ç¼©è¿›
set signcolumn=auto
set tabstop=4      "tabé”®é•¿åº¦
set expandtab      "tabæ‰©å±•ä¸ºç©ºæ ¼
set shiftwidth=4   "ç¼©è¿›é•¿åº¦
set softtabstop=4  "ç¼©è¿›é•¿åº¦
set smarttab       "æ™ºèƒ½ç¼©è¿›
set ignorecase     "æœç´¢å¿½ç•¥å¤§å°å†™
set incsearch      "æœç´¢åŠ å¼º
set hlsearch       "æœç´¢é«˜äº®
set showmatch      "è‡ªåŠ¨åŒ¹é…
set matchtime=1    "åŒ¹é…æ‹¬å·é«˜äº®çš„æ—¶é—´
set viminfo=       "ç¦ç”¨viminfo
set wildmenu       "å‘½ä»¤è¡Œå¢å¼ºè¡¥å…¨æ˜¾ç¤º
set noautochdir    "ç¦ç”¨è‡ªåŠ¨åˆ‡pwdåˆ°æ¢æ–‡ä»¶è·¯å¾„
set diffopt=vertical,filler
set bsdir=buffer 
set ffs=unix,dos,mac  "æ¢è¡Œæ ¼å¼é›†
set mouse=a           "è®¾ç½®é¼ æ ‡èŒƒå›´
set laststatus=2      "å§‹ç»ˆæ˜¾ç¤ºçŠ¶æ€æ 
set completeopt=menu,menuone,noinsert,preview,noselect
" gcc/g++
set errorformat=%f:%l:%c:\ %m
" verilog
set errorformat+=**\ Error:\ (vlog-%*\\d)\ %f(%l):\ %m
set errorformat+=**\ Error:\ (vlog-%*\\d)\ %f(%l.%c):\ %m
set errorformat+=**\ Error:\ (suppressible):\ %f(%l.%c):\ %m
set errorformat+=**\ Error:\ %f(%l):\ %m
set errorformat+=**\ Error:\ %f(%l.%c):\ %m
set errorformat+=**\ at\ %f(%l):\ %m
set errorformat+=**\ at\ %f(%l.%c):\ %m
"set foldmethod=syntax "æŠ˜å æ–¹å¼ï¼ˆä¾æ®è¯­æ³•ï¼‰
"set foldcolumn=1     "æŠ˜å çº§åˆ«æ˜¾ç¤º
"set foldlevel=1      "æŠ˜å çº§åˆ«
colorscheme cydark
set helplang=cn
set langmenu=zh_CN.UTF-8
set enc=utf-8
set fencs=utf-8,gb18030,gbk,gb2312,big5,ucs-bom,shift-jis,utf-16,latin1
set statusline=[%{mode('2')}]\ %f%m%r%h%w%<%=
set statusline+=%{ALEGetStatusLine()}%5(\ %)
set statusline+=%{''.(&fenc!=''?&fenc:&enc).''}%{(&bomb?\",BOM\":\"\")}\ â”‚\ %{&ff}\ â”‚\ %Y%5(\ %)
set statusline+=%-10.(%l:%c%V%)\ %4P%(\ %)

"è‡ªå®šä¹‰å‘½ä»¤/è‡ªåŠ¨å‘½ä»¤=====================
augroup UsrDefCmd
    autocmd!
    autocmd QuickFixCmdPost * copen 10
    autocmd BufRead,BufNewFile *.vt,*.vo,*.vg set filetype=verilog
    autocmd BufRead,BufNewFile *.sv set filetype=systemverilog
    autocmd BufRead,BufNewFile *.d set filetype=make
augroup END

command! -range=% CFormat :<line1>,<line2>call CodeFormat()
command! -range RComment :<line1>,<line2>call ReverseComment()
command! -range=% DBLank :<line1>,<line2>s/\s\+$//ge|<line1>,<line2>s/\(\s*\n\+\)\{3,}/\="\n\n"/ge|silent! /@#$%^&* 
command! Qs call BMBPSign_SaveWorkSpace('') | wqall

command! -nargs=* Amake :AsyncRun make
command! Actags :Async ctags -R -f .tags
command! Avdel :Async vdel -lib work -all

"å¿«æ·é”®æ˜ å°„=====================
" æ‹¬å·å¼•å·è‡ªåŠ¨è¡¥å…¨
inoremap ( ()<Esc>i
inoremap ) <c-r>=ClosePair(')')<CR>
inoremap [ []<Esc>i
inoremap ] <c-r>=ClosePair(']')<CR>
inoremap { {}<Esc>i
inoremap } <c-r>=ClosePair('}')<CR>
"inoremap ' ''<Esc>i
inoremap " ""<Esc>i
" æ›´æ”¹PWDåˆ°å½“å‰æ–‡ä»¶æ‰€åœ¨ç›®å½•
nmap \cd :exec 'cd ' . expand('%:h') . '\|pwd'<CR>
nmap \od :Async xdg-open .<CR>
nmap \of :Async xdg-open %<CR>
nmap \rf :exec 'Async xdg-open ' . expand('%:h')<CR>
vmap \cf :call CodeFormat()<CR>
nmap \h  :call HEXCovent()<CR>
nmap <silent> \q :call ReverseComment()<CR>
vmap <silent> \q :call ReverseComment()<CR>

" æŸ¥æ‰¾
vmap <C-f> yk:exec '/' . getreg('0')<CR><BS>n
nmap <C-f> wbve<C-f>
imap <C-f> <Esc>lwbve<C-f>
" æŸ¥æ‰¾å¹¶æ›¿æ¢
vmap <C-h> y:call StrSubstitute(getreg('0'))<CR>
nmap <C-h> wbve<C-h>
imap <C-h> <Esc>lwbve<C-h>

map  <C-a> <Esc>ggvG$
map  <C-w> <Esc>:close<CR>
map  <S-PageUp> <Esc>:wincmd W<CR>
map  <S-pageDown> <Esc>:wincmd w<CR>
map  <C-t> <Esc>:tabnew<CR>
map  <S-t> <Esc>:tabclose<CR>
map  <S-tab> <Esc>:tabnext<CR>
map! <C-a> <Esc><C-a>
map! <C-w> <Esc><C-w>
map! <S-PageUp> <Esc><S-PageUp>
map! <S-PageDown> <Esc><S-PageDown>
map! <C-t> <Esc><C-t>
map! <S-tab> <Esc><S-tab>
" ä¿å­˜å¿«æ·é”®
map  <f3> <Esc>:call SaveSpecifiedFile(expand('%'))<CR> 
map! <f3> <Esc><f3>
map  <silent> <f4> :call WinResize()<Cr>
map! <f4> <Esc><f4>
" çª—å£åˆ‡æ¢
map  <f7> <Esc>:call GIT_Toggle()<CR>
map  <f8> <Esc>:call ToggleTagbar()<CR>
map  <f9> <Esc>:call ToggleNERDTree()<CR>
map  <f10> <ESC>:call ToggleQuickFix()<CR>
map! <f7> <Esc><f7>
map! <f8> <Esc><f8>
map! <f9> <Esc><f9>
map! <f10> <ESC><f10>
" ç¼–è¯‘æ‰§è¡Œ
map  <silent> <f5> <Esc>:call CompileRun()<CR>
map! <silent> <f5> <Esc><f5>
" æ–­ç‚¹ BMBPSign.vim
map  <silent> <f6> <Esc>:BMBPSignToggleBreakPoint<CR>
map  <silent> \b <Esc>:BMBPSignClearBreakPoint<CR>
map! <silent> <f6> <Esc><f6>
" ä¹¦ç­¾ BMBPSign.vim
map  <silent> <f12> <Esc>:BMBPSignToggleBookMark<CR>
map  <silent> <C-Down> <Esc>:BMBPSignNextBookMark<CR>
map  <silent> <C-Up> <Esc>:BMBPSignPreviousBookMark<CR>
map  <silent> \m <Esc>:BMBPSignClearBookMark<CR>
map! <silent> <f12> <Esc><f12>
map! <silent> <C-Down> <Esc><C-Down>
map! <silent> <C-Up> <Esc><C-Up>

"æ’ä»¶é…ç½®======================
"  Netrw-NERDTree é…ç½®
let g:netrw_winsize=-25
let g:netrw_dirhistmax=0
let g:netrw_browse_split=4
let g:netrw_altv=1
let g:netrw_banner=0
let g:netrw_liststyle=3

let g:NERDTreeStatusline='[NERDTree]'
let g:NERDTreeAutoDeleteBuffer=1
let g:NERDTreeMouseMode=2

" Add "call NERDTreeAddKey_Menu_Def()" to ~/.vim/plugin/NERD_tree
function! NERDTreeAddKey_Menu_Def()
    call NERDTreeAddMenuItem({
                \ 'text': 'Switch file (x) permission',
                \ 'shortcut': 'x',
                \ 'callback': 'SwitchXPermission'
                \ })

    call NERDTreeAddKeyMap({
                \ 'key': 'dbg',
                \ 'callback': 'DebugFile',
                \ 'quickhelpText': 'Debug file by gdb tool',
                \ 'scope': 'Node'
                \ })
endfunction

function! SwitchXPermission()
    let l:currentNode = g:NERDTreeFileNode.GetSelected()
    if getfperm(l:currentNode.path.str())[2] == 'x'
        call system("chmod -x '" . l:currentNode.path.str() . "'")
    else
        call system("chmod +x '" . l:currentNode.path.str() . "'")
    endif
    silent call nerdtree#ui_glue#invokeKeyMap('R')
endfunction

function! DebugFile(node)
    call INTERACTIVE_Start(a:node.path.str(), 'dbg')
endfunction

"  TagBar é…ç½®
let g:tagbar_width=31
let g:tagbar_vertical=19
let g:tagbar_silent=1
let g:tagbar_left=0
"  TagBar å…¶ä»–è¯­è¨€æ”¯æŒ
"    Makefile
let g:tagbar_type_make = {
            \ 'kinds':[
            \ 'm:macros',
            \ 't:targets'
            \ ]
            \}
"    Markdown
let g:tagbar_type_markdown = {
            \ 'ctagstype' : 'markdown',
            \ 'kinds' : [
            \ 'h:Heading_L1',
            \ 'i:Heading_L2',
            \ 'k:Heading_L3'
            \ ]
            \ }

" ale
let g:ale_sign_error = 'ğŸ‘½'
let g:ale_sign_warning = 'ğŸ’¡'
let g:ale_echo_msg_error_str = 'E'
let g:ale_echo_msg_warning_str = 'W'
let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'
let g:ale_statusline_format = ['Ã— %d', '! %d', 'â¬¥ ok']
let g:ale_sign_column_always = 1
"let g:ale_lint_delay = 1000
let g:ale_lint_on_text_changed = 'normal'
let g:ale_set_loclist = 0
let g:ale_set_quickfix = 1
"================================
" BMBPSign configure =================
let g:BMBPSign_SpecialBuf = {
            \ 'NERD_tree': 'call ToggleNERDTree()',
            \ '__Tagbar': 'call ToggleTagbar()|wincmd W'
            \ }

let g:BMBPSign_ProjectType = {
                \ 'c':       '~/Documents/WorkSpace',
                \ 'cpp':     '~/Documents/WorkSpace',
                \ 'fpga':    '~/Documents/Altera',
                \ 'verilog': '~/Documents/Modelsim',
                \ 'altera':  '~/Documents/Altera',
                \ 'xilinx':  '~/Documents/Xilinx',
                \ 'default': '~/Documents'
                \ }

"################### è‡ªå®šä¹‰å‡½æ•° #############################
"	ç¼–è¯‘è¿è¡Œ: F5
function! CompileRun()
    wall
    if &filetype == 'nerdtree'
        silent call nerdtree#ui_glue#invokeKeyMap('R')
        echo 'Refresh Done!'
    elseif &filetype =~ '^git\(log\|commit\|status\|branch\)$'
        silent call GIT_Refresh()
    elseif filereadable('makefile') || filereadable('Makefile')
        AsyncRun make
    elseif &filetype =~ '^c\|cpp$'
        AsyncRun g++ -Wall -O0 -g3 % -o binFile
    elseif &filetype == 'verilog'
        if isdirectory('work')
            exec 'AsyncRun vlog -work work %'
        else
            exec 'AsyncRun vlib work && vmap work work && vlog -work work %'
        endif
    endif
endfunction

"	è½¬æ¢é¼ æ ‡èŒƒå›´ï¼ˆaï¼Œvï¼‰: \c
function! CutMouseBehavior()
    if &mouse == 'a'
        set nonumber
        set mouse=v
    else
        set number
        set mouse=a
    endif
endfunction

"  )]}è‡ªåŠ¨è¡¥å…¨ç›¸å…³
function! ClosePair(char)
    if getline('.')[col('.') - 1] == a:char
        return "\<Right>"
    else
        return a:char
    endif
endfunction

"	æŒ‡å®šèŒƒå›´ä»£ç æ ¼å¼åŒ–: CFormat
function! CodeFormat()
    if &filetype =~ '^c\|cpp$'
        silent! s/\(\w\|)\|\]\)\s*\([-+=*/%><|&:!?^][-+=/><|&:]\?=\?\)\s*/\1 \2 /ge
        silent! s/\s*\(++\|--\|::\)\s*/\1/ge
        silent! s/\s*\(<\)\s*\(.\+\w\+\)\s*\(>\)\s*/ \1\2\3 /ge
        silent! s/\((\)\s*\|\s*\()\)/\1\2/ge
        silent! s/\(,\|;\)\s*\(\w\)/\1 \2/ge
        silent! s/\(\s*\n*\s*\)*{/ {/ge
        silent! s/\(\s*\n\+\)\{3,}/\="\n\n"/ge
        normal ==
    elseif &filetype == 'matlab'
        silent! s/\(\w\|)\)\s*\(\.\?[-+=*/><~|&^][=&|]\?\)\s*/\1 \2 /ge
        silent! s/\((\)\s*\|\s*\()\)/\1\2/ge
        silent! s/\(;\)\s*\(\w\)/\1 \2/ge
        silent! s/\(\s*\n\+\)\{3,}/\="\n\n"/ge
        normal ==
    elseif &filetype == 'make'
        silent! s/\(\w\)\s*\(+=\|=\|:=\)\s*/\1 \2 /ge
        silent! s/\(:\)\s*\(\w\|\$\)/\1 \2/ge
        silent! s/\(\s*\n\+\)\{3,}/\="\n\n"/ge
        normal ==
    elseif &filetype == 'python'
        silent! s/\(\w\|)\|\]\|}\)\s*\([-+=*/%><|&~!^][=*/><]\?=\?\)\s*/\1 \2 /ge
        silent! s/\((\|\[\|{\)\s*\|\s*\()\|\]\|}\)/\1\2/ge
        silent! s/\(,\|;\)\s*\(\w\)/\1 \2/ge
        silent! s/\(\s*\n\+\)\{3,}/\="\n\n"/ge
        normal ==
    elseif &filetype =~ '^verilog\|systemverilog$'
        silent! s/\(\w\|)\|\]\)\s*\([-+=*/%><|&!?~^][=><|&~]\?\)\s*/\1 \2 /ge
        silent! s/\((\)\s*\|\s*\()\)/\1\2/ge
        silent! s/\(,\|;\)\s*\(\w\)/\1 \2/ge
        silent! s/\(\s*\n\+\)\{3,}/\="\n\n"/ge
    endif
    silent! /`!`!`!`!`@#$%^&
endfunction

"   åˆ‡æ¢æ³¨é‡ŠçŠ¶æ€: \q
function! ReverseComment()
    if &filetype =~ '^c\|cpp\|verilog\|systemverilog$'
        let l:char='//'
    elseif &filetype == 'matlab'
        let l:char='%'
    elseif &filetype =~ '^sh\|make\|python$'
        let l:char='#'
    elseif &filetype == 'vim'
        let l:char="\""
    else
        return
    endif
    exec 's+^+' . l:char . '+e'
    exec 's+^' . l:char . l:char . '++e'
endfunction

"  åˆ·æ–°ç›®å½•æ ‘
function! UpdateNERTreeView()
    let l:nrOfNerd_tree=bufwinnr('NERD_tree')
    if l:nrOfNerd_tree != -1
        let l:id=win_getid()
        exec l:nrOfNerd_tree . 'wincmd w'
        silent call nerdtree#ui_glue#invokeKeyMap('R')
        call win_gotoid(l:id)
    endif
endfunction

" å­—ç¬¦ä¸²æŸ¥æ‰¾æ›¿æ¢: ctrl+h
function! StrSubstitute(str)
    let l:pos = getpos('.')
    let l:subs=input('Replace ' . "\"" . a:str . "\"" . ' with: ')
    if l:subs != ''
        exec '%s/' . a:str . '/' . l:subs . '/Ig'
        call setpos('.', l:pos)
    endif
endfunction

" æ–‡ä»¶ä¿å­˜åŠåæœŸå¤„ç†: F3
function! SaveSpecifiedFile(file)
    if exists('g:DoubleClick_500MSTimer')
        wall
        echo 'Save all'
    elseif filereadable(a:file)
        let g:DoubleClick_500MSTimer = 1
        let l:id = timer_start(500, 'TimerHandle500MS')
        write
    elseif empty(a:file)
        exec 'file ' . input('Set file name')
        filetype detect
        write
        call UpdateNERTreeView()
    else
        write
    endif
endfunction

function! TimerHandle500MS(id)
    unlet g:DoubleClick_500MSTimer
endfunction

" åˆ‡æ¢16è¿›åˆ¶æ˜¾ç¤º: \h
function! HEXCovent()
    if empty(matchstr(getline(1), '^00000000: \S'))
        :%!xxd
        let b:ale_enabled = 0
    else
        :%!xxd -r
        let b:ale_enabled = 1
    endif
endfunction

" æœ€å¤§åŒ–çª—å£/æ¢å¤ï¼šf4
function! WinResize()
    let l:winId = win_getid()
    if !exists('g:MAXMIZEWIN')
        let g:MAXMIZEWIN = [winheight(0), winwidth(0), l:winId]
        exec 'resize ' . max([float2nr(0.8 * &lines), g:MAXMIZEWIN[0]])
        exec 'vert resize ' . max([float2nr(0.8 * &columns), g:MAXMIZEWIN[1]])
    elseif g:MAXMIZEWIN[2] == l:winId
        exec 'resize ' . g:MAXMIZEWIN[0]
        exec 'vert resize ' . g:MAXMIZEWIN[1]
        unlet g:MAXMIZEWIN
    else
        if win_gotoid(g:MAXMIZEWIN[2]) == 1
            exec 'resize ' . g:MAXMIZEWIN[0]
            exec 'vert resize ' . g:MAXMIZEWIN[1]
            call win_gotoid(l:winId)
        endif
        let g:MAXMIZEWIN = [winheight(0), winwidth(0), l:winId]
        exec 'resize ' . max([float2nr(0.8 * &lines), g:MAXMIZEWIN[0]])
        exec 'vert resize ' . max([float2nr(0.8 * &columns), g:MAXMIZEWIN[1]])
    endif
endfunction

" ############### çª—å£ç›¸å…³ ######################################
"  åˆ‡æ¢NERDTreeçª—å£: F9
function! ToggleNERDTree()
    if bufwinnr('NERD_tree') != -1
        NERDTreeClose
    elseif bufwinnr('Tagbar') != -1
        TagbarClose
        NERDTree
        TagbarOpen
    else
        NERDTree
    endif
endfunction

"  åˆ‡æ¢TagBarçª—å£: F8
function! ToggleTagbar()
    let l:id=win_getid()
    if bufwinnr('Tagbar') != -1
        TagbarClose
    elseif bufwinnr('NERD_tree') == -1
        let g:tagbar_vertical=0
        let g:tagbar_left=1
        TagbarOpen
        let g:tagbar_vertical=19
        let g:tagbar_left=0
        call win_gotoid(l:id)
    else
        exec bufwinnr('NERD_tree') . 'wincmd w'
        TagbarOpen
        call win_gotoid(l:id)
    endif
endfunction

"  åˆ‡æ¢QuickFixçª—å£: F10
function! ToggleQuickFix()
    let l:id=win_getid()
    exec tabpagewinnr(tabpagenr(),'$') . 'wincmd w'
    if &ft == 'qf'
        cclose
    else
        copen 10
        call win_gotoid(l:id)
    endif
endfunction
"#####################################################################

